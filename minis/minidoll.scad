
//
// doll.scad
//

include <minilib.scad>;


function vor(vector, index, default) =
  vector[index] ? vector[index] : default;


function body_points(
  height,
  spine_vectors=[],
  left_leg_vectors=[],
  right_leg_vectors=[],
  left_arm_vectors=[],
  right_arm_vectors=[],
  basin_ratio=0,
  waist_ratio=0,
  shoulder_ratio=0
) =
  let(

    br =
      basin_ratio == 0 ? 1.2 :
      basin_ratio == "male" ? 1.2 :
      basin_ratio == "female" ? 1.7 :
      basin_ratio,
    wr =
      waist_ratio == 0 ?  1.1 :
      waist_ratio,
    sr =
      shoulder_ratio == 0 ?  2.1 :
      shoulder_ratio == "male" ? 2.3 :
      shoulder_ratio == "female" ? 2.1 :
      shoulder_ratio,

    hh = height / 8, // head height
    bw2 = hh * br / 2, // basin width / 2
    sw2 = hh * sr / 2, // shoulder width / 2
    ww2 = hh * wr / 2, // wast width / 2
    fl = hh * 0.7, // foot length
    hl = hh * 0.6, // hand length

    svs = spine_vectors,
    sp0 = [ 0, 0, 0 ],
    sp1 = to_xyz(hh, vor(svs, 0, [ 90, 0 ]), sp0),
    sp2 = to_xyz(1.5 * hh, vor(svs, 2, [ 90, 0 ]), sp1),
    sp3 = to_xyz(0.5 * hh, vor(svs, 3, [ 90, 0 ]), sp2),
    sp4 = to_xyz(0.5 * hh, vor(svs, 4, [ 90, 0 ]), sp3),
      //
    sp1h = to_xyz(0.7 * hh, vor(svs, 0, [ 90, 0 ]), sp0),
    wal = to_xyz(ww2, vor(left_leg_vectors, 0, [ 0, 90 ]), sp1h),
    war = to_xyz(ww2, vor(left_leg_vectors, 0, [ 0, -90 ]), sp1h),
      // TODO bring back somehow

    llvs = left_leg_vectors,
    llp0 = to_xyz(bw2, vor(llvs, 0, [ 0, 90 ]), sp0),
    llp1 = to_xyz(2 * hh, vor(llvs, 1, [ -90, 0 ]), llp0),
    llp2 = to_xyz(2 * hh, vor(llvs, 2, [ -90, 0 ]), llp1),
    llp3 = to_xyz(fl, vor(llvs, 3, [ 0, 0 ]), llp2),

    rlvs = right_leg_vectors,
    rlp0 = to_xyz(bw2, vor(rlvs, 0, [ 0, -90 ]), sp0),
    rlp1 = to_xyz(2 * hh, vor(rlvs, 1, [ -90, 0 ]), rlp0),
    rlp2 = to_xyz(2 * hh, vor(rlvs, 2, [ -90, 0 ]), rlp1),
    rlp3 = to_xyz(fl, vor(rlvs, 3, [ 0, 0 ]), rlp2),

    lavs = left_arm_vectors,
    lap0 = to_xyz(sw2, vor(lavs, 0, [ 0, 90 ]), sp2),
    lap1 = to_xyz(1.5 * hh, vor(lavs, 1, [ -90, 0 ]), lap0),
    lap2 = to_xyz(1.3 * hh, vor(lavs, 2, [ -90, 0 ]), lap1),
    lap3 = to_xyz(hl, vor(lavs, 3, [ -90, 0 ]), lap2),

    ravs = right_arm_vectors,
    rap0 = to_xyz(sw2, vor(ravs, 0, [ 0, -90 ]), sp2),
    rap1 = to_xyz(1.5 * hh, vor(ravs, 1, [ -90, 0 ]), rap0),
    rap2 = to_xyz(1.3 * hh, vor(ravs, 2, [ -90, 0 ]), rap1),
    rap3 = to_xyz(hl, vor(ravs, 3, [ -90, 0 ]), rap2),

    z0 = llp3.z,
    z1 = rlp3.z,
    z = - (z0 < z1 ? z0 : z1)

  ) [
    [ sp0, sp1, sp2, sp3, sp4 ], // spine points
    [ wal, sp1h, war ], // waist points
    [ llp0, llp1, llp2, llp3 ], // left leg points
    [ rlp0, rlp1, rlp2, rlp3 ], // right leg points
    [ lap0, lap1, lap2, lap3 ], // left arm points
    [ rap0, rap1, rap2, rap3 ], // right arm points
    z, // ground to start z
    hh, // head height
    [ "hh", hh ] // debug
  ];


module bal(p, d, fn=0) {
  _fn = fn == 0 ? $fn : fn;
  translate(p) sphere(d, $fn=_fn);
};
module hul(p0, d0, p1, d1) {
  hull() { bal(p0, d0); bal(p1, d1); }
}


module body(
  body_points, // as generated by the body_points() function
  foot_diameter=0,
  ankle_diameter=0,
  calf_diameter=0,
  knee_diameter=0,
  thigh_diameter=0,
  buttock_diameter=0,
  shoulder_diameter=0,
  neck_diameter=0,
  elbow_diameter=0,
  wrist_diameter=0,
  palm_diameter=0,
  fn=0
) {

  _fn = fn == 0 ? $fn : fn;
  sps = body_points[0]; // spine points
  wps = body_points[1]; // waist points
  llps = body_points[2]; // left leg points
  rlps = body_points[3]; // right leg points
  laps = body_points[4]; // left arm points
  raps = body_points[5]; // right arm points
  z = body_points[6]; // foot to start point (basin) z distance
  hh = body_points[7]; // head height

  d = hh / 4; // default diameter

  fd = foot_diameter > 0 ? foot_diameter : d;
  ad = ankle_diameter > 0 ? ankle_diameter : d;
  //cd = calf_diameter > 0 ? calf_diameter : d;
  kd = knee_diameter > 0 ? knee_diameter : d;
  //td = thigh_diameter > 0 ? thigh_diameter : d;
  bd = buttock_diameter > 0 ? buttock_diameter : d;
  sd = shoulder_diameter > 0 ? shoulder_diameter : d;
  nd = neck_diameter > 0 ? neck_diameter : d;
  ed = elbow_diameter > 0 ? elbow_diameter : d;
  wd = wrist_diameter > 0 ? wrist_diameter : d;
  pd = palm_diameter > 0 ? palm_diameter : d;

  hul(llps[0], bd, llps[1], kd); // TODO replace buttock
  hul(llps[1], kd, llps[2], ad);
  hul(llps[2], ad, llps[3], fd);
  hul(rlps[0], bd, rlps[1], kd); // TODO replace buttock
  hul(rlps[1], kd, rlps[2], ad);
  hul(rlps[2], ad, rlps[3], fd);

  hul(llps[0], bd, sps[0], bd); // TODO replace buttock
  hul(sps[0], bd, rlps[0], bd); // TODO replace buttock

  hul(sps[0], d, sps[1], d);
  hul(sps[1], d, sps[2], d);
  hul(sps[2], d, sps[3], d);
  hul(sps[3], d, sps[4], d);

  hul(laps[0], sd, sps[2], d);
  hul(sps[2], d, raps[0], sd);

  hul(laps[0], sd, laps[1], ed);
  hul(laps[1], ed, laps[2], wd);
  hul(laps[2], wd, laps[3], pd);
  hul(raps[0], sd, raps[1], ed);
  hul(raps[1], ed, raps[2], wd);
  hul(raps[2], wd, raps[3], pd);

  hull() {
    bal(llps[0], d); bal(rlps[0], d);
    bal(wps[0], d); bal(wps[2], d);
  }
  hull() {
    bal(wps[0], d); bal(wps[2], d);
    bal(laps[0], d); bal(sps[2], d); bal(raps[0], d);
  }

  //scale([ 0.8, 1, 1 ])
  //  bal(sps[4], hh * 0.6); // head
}


module robe(
  body_points, // as generated by the body_points() function
  diameter=0
) {

  hh = body_points[7]; // head height
  //sps = body_points[0]; // spine points
  llps = body_points[2]; // left leg points
  rlps = body_points[3]; // right leg points
  laps = body_points[4]; // left arm points
  raps = body_points[5]; // right arm points
  z = body_points[6]; // start point altitude

  d = diameter > 0 ? diameter : hh / 4;

  //function to_ground(p) =
  //  [ p.x, p.y, -z ];

  hull() {
    bal(llps[0], d);
    bal(llps[1], d);
    bal(llps[2], d);
    bal(rlps[0], d);
    bal(rlps[1], d);
    bal(rlps[2], d);

    //bal(to_ground(laps[1]), d);
    //bal(to_ground(laps[2]), d);
    //bal(to_ground(raps[1]), d);
    //bal(to_ground(raps[2]), d);

    bal(laps[0], d);
    bal(laps[1], d);
    bal(laps[2], d);
    bal(laps[3], d);
    bal(raps[0], d);
    bal(raps[1], d);
    bal(raps[2], d);
    bal(raps[3], d);
  }
}


module skull(body_points, fn=0) {

  _fn = fn == 0 ? $fn : fn;

  sps = body_points[0]; // spine points
  sps4 = sps[4];
  hh = body_points[7]; // head height

  difference() {
    union() {
      translate([ sps4.x, sps4.y + hh / 10, sps4.z - hh / 3 ])
        cylinder(d=hh/1.3, h=hh/3, center=true, $fn=_fn);
      translate([ 0, 0, hh / 7 ])
        scale([ 0.8, 1, 1 ])
          bal(sps4, hh * 0.6, _fn); // head
    }
    translate([ - hh / 3.7, hh / 2.1, hh / 7 ])
      bal(sps4, hh * 0.16, _fn); // eyesocket
    translate([ hh / 3.7, hh / 2.1, hh / 7 ])
      bal(sps4, hh * 0.16, _fn); // eyesocket
  }
};


echo("======================================================================");


$fn = 24;

bps = body_points(
  33,
  //[ 80, 80, 80, 70 ], // spine vectors
  [], // spine vectors
  //[ undef, -80, -150, -45 ], // left leg vectors
  [ [], -85, -95, [ 0, 10 ] ], // left leg vectors
  [ [], -95, -100, [ 0, -10 ] ], // right leg vectors
  [ [ 0, 90 ], [ -45, 90 ], [ -45, -90 ], [ -25, -100 ] ], // left arm vectors
  [ [ 0, -100 ], [ -45, -90 ], [ -45, 90 ], [ 207, -110 ] ]); // right arm vectors
echo(bps);

d = [ 0, 0, bps[6] ];

//#base();
//
//echo(concat("spine points", bps[0]));
//for (sp = bps[0]) translate(d + sp) sphere(0.7);
//
//echo(concat("waist points", bps[1]));
//for (wp = bps[1]) translate(d + wp) sphere(0.6);
//
//echo(concat("left leg points", bps[2]));
//for (llp = bps[2]) translate(d + llp) color("blue", 0.6) sphere(0.5);
//
//echo(concat("right leg points", bps[3]));
//for (rlp = bps[3]) translate(d + rlp) color("red", 0.6) sphere(0.5);
//
//echo(concat("left arm points", bps[4]));
//for (lap = bps[4]) translate(d + lap) color("red", 0.6) sphere(0.5);
//
//echo(concat("right arm points", bps[5]));
//for (rap = bps[5]) translate(d + rap) color("blue", 0.6) sphere(0.5);

//echo ("-------------------------------------------------------------------");
//translate(d + sps[3]) color("black") sphere(1);
//echo([ "hh", 33 / 8 ]);
//echo([ "sp3", sps[3] ]);
//echo([ "sp3 spherical", to_spherical(sps[3]) ]);

translate([ -25, 0, 0 ]) {
  base(text=" A");
  translate([ 0, 0, bps[6] ]) {
    body(bps);
    robe(bps);
    skull(bps);
  }
}

